(
/*function to enforce any arrays to be strings */
$to_str := function($x){
  $type($x)="array" ? $join($x,';,;') : $x
};

$extract_media := function($x){
  $type($x)="array" ? $join($x.url, ',') : $x
};

$truncate := function($x,$length){
    $x ? $substring($x,0,$length) : null
};

{
  "required": {
    "gatewayId": extra.id ? extra.id : $truncate(input.identifier,50),
    "gatewayPid": extra.pid ? extra.pid : input.identifier,
    "issued": input.issued,
    "modified": input.modified,
    "version": input.version,
    "revisions": input.revisions ? [$map(input.revisions,function($r) {
      {
        "version": $r.version,
        "url": $r.url
      }
    })] : []
  },
  "summary": {
    "abstract": $truncate(input.summary.abstract,5000),
    "contactPoint": input.summary.contactPoint,
    "keywords": $to_str(input.summary.keywords),
    "controlledKeywords": $to_str([$filter( $split($to_str($keywords),','), function($v) {
      $v in extra.controlledKeywords
    })]),
    "datasetType": input.provenance.origin.datasetType ? input.provenance.origin.datasetType :
        extra.datasetType ? extra.datasetType : null,
    "datasetSubType": input.provenance.origin.datasetSubType ? input.provenance.origin.datasetSubType :
        extra.datasetSubType ? extra.datasetSubType : null,
    "populationSize": input.summary.populationSize,
    "description": $truncate(input.documentation.description,50000),
    "doiName": input.summary.doiName,
    "shortTitle": $truncate(input.summary.title,100),
    "title": $truncate(input.summary.title,255),
    "publisher": {
      "name": extra.publisherName ? extra.publisherName : input.summary.dataProvider.name,
      "gatewayId": extra.publisherId ? extra.publisherId : $truncate(input.summary.dataProvider.identifier, 50)
    },
    "inPipeline": input.documentation.inPipeline ? input.documentation.inPipeline : null
  },
  "coverage": {
    "pathway": input.coverage.pathway, 
    "spatial": $to_str(input.coverage.spatial),
    "followup": input.coverage.followup,
    "typicalAgeRange": $join([$string(input.coverage.typicalAgeRangeMin), "-", $string(input.coverage.typicalAgeRangeMax)]),
    "datasetCompleteness": input.coverage.datasetCompleteness
  },
  "provenance": {
    "origin": {
      "purpose": $to_str(input.provenance.origin.purpose),
      "source": $to_str(input.provenance.origin.source),
      "collectionSituation": $to_str(input.provenance.origin.collectionSource),
      "imageContrast": input.provenance.origin.imageContrast
    },
    "temporal": {
      "endDate": input.provenance.temporal.endDate,
      "startDate": input.provenance.temporal.startDate,
      "timeLag": $uppercase(input.provenance.temporal.timeLag),
      "accrualPeriodicity": $uppercase(input.provenance.temporal.publishingFrequency),
      "distributionReleaseDate": input.provenance.temporal.distributionReleaseDate
    }
  },
  "accessibility": {
    "access": {
      "deliveryLeadTime": $to_str(input.accessibility.access.deliveryLeadTime), /*may need to be min/max in the future */
      "jurisdiction": $to_str(input.accessibility.access.jurisdiction),
      "dataController": input.accessibility.access.dataController,
      "dataProcessor": input.accessibility.access.dataProcessor,
      "accessRights": input.accessibility.access.accessRights ~> $to_str,
      "accessService": input.accessibility.access.accessService,
      "accessRequestCost": $to_str(input.accessibility.access.accessRequestCost), /* may need to be fixed */
      "accessServiceCategory": $to_str(input.accessibility.access.accessServiceCategory),
      "accessMode": input.accessibility.access.accessMode
    },
    "usage": {
      "dataUseLimitation": input.accessibility.usage.dataUseLimitation ? 
        $to_str(input.accessibility.usage.dataUseLimitation) : null,	
      "dataUseRequirement": input.accessibility.usage.dataUseRequirements ? 
        $to_str(input.accessibility.usage.dataUseRequirements) : null,
      "resourceCreator": {
            "name": input.accessibility.usage.resourceCreator ?
                $to_str(input.accessibility.usage.resourceCreator) : null
      }
    },
    "formatAndStandards": {
      "vocabularyEncodingSchemes": input.accessibility.formatAndStandards.vocabularyEncodingScheme ? 
        $to_str(input.accessibility.formatAndStandards.vocabularyEncodingScheme) : null,
      "conformsTo": input.accessibility.formatAndStandards.conformsTo ? 
        $to_str(input.accessibility.formatAndStandards.conformsTo) : null,
      "languages": input.accessibility.formatAndStandards.language ? 
        $to_str(input.accessibility.formatAndStandards.language) : null,
      "formats": input.accessibility.formatAndStandards.format ? 
        $to_str(input.accessibility.formatAndStandards.format) : null
    }
  },
  "linkage": {
    "isGeneratedUsing": extra.isGeneratedUsing ? extra.isGeneratedUsing : null,
    "associatedMedia": $extract_media(input.documentation.associatedMedia),
    "dataUses": extra.dataUses ? extra.dataUses : null,
    "isReferenceIn": null,
    "tools": $to_str(input.enrichmentAndLinkage.tools),
    "datasetLinkage":{
      "isDerivedFrom":$to_str(input.enrichmentAndLinkage.derivedFrom),
      "isPartOf":$to_str(input.enrichmentAndLinkage.isPartOf),
      "isMemberOf": $to_str(input.enrichmentAndLinkage.similarToDatasets),
      "linkedDatasets":$to_str(input.enrichmentAndLinkage.linkableDatasets)
    },
    "investigations":$to_str(input.enrichmentAndLinkage.investigations),
    "syntheticDataWebLink": $to_str(input.structuralMetadata.syntheticDataWebLink),
    "publicationAboutDataset": input.enrichmentAndLinkage.publicationAboutDataset,
    "publicationUsingDataset": input.enrichmentAndLinkage.publicationUsingDataset
  },
  "observations": input.observations ? [$map(input.observations,function($r) {
      {
        "observedNode": $r.observedNode,
        "measuredValue": $r.measuredValue,
        "observationDate": $r.observationDate,
        "measuredProperty": $r.measuredProperty,
        "disambiguatingDescription": $r.disambiguatingDescription
      }
    })] : [],
  "structuralMetadata": input.structuralMetadata.tables,
  "tissuesSampleCollection": input.coverage.materialType ? [$map(input.coverage.materialType,function($type) {
      {
        "materialType": $type
      }
    })] : []
};
)