(
/* define my custom functions */

/* make sure to convert dates into ISO-8601 */
$to_iso8601 := function($x){
    $replace($x, /(\.\d{6})$/, 'Z')
};


$to_str := function($x){
    $type($x) = 'array' ? $join($x,",") : $x
};

{
    "@context": "https://schema.org/",
    "@id": "https://hdruk.ac.uk",
    "@type": "Dataset",
    "identifier": [input.required.gatewayPid,input.summary.doiName? input.summary.doiName], /*can be a list of URL or Text*/
    "version": input.required.version,
    "url":"https://hdruk.ac.uk/"&input.required.gatewayId, /*should use url to link to the dataset on our gateway */
    "name": input.summary.title,
    "alternateName": input.summary.shortTitle,
    "description":input.summary.description, 
    "citation":input.summary.doiName,
    "creator": {
        "@type": "Organization", 
        "legalName": input.accessibility.usage.resourceCreator,
        "name": input.accessibility.usage.resourceCreator,
        "email": input.summary.contactPoint
    },
    "maintainer": {
        "@type": "Organization", 
        "legalName": input.accessibility.access.dataController,
        "name": input.accessibility.access.dataController,
        "email": input.summary.contactPoint /*this needs to be improved */
    },
    "publisher": {
        "@type": "Organization", 
        "legalName": input.summary.publisher.name,
        "name": input.summary.publisher.name,
        "identifier":input.summary.publisher.gatewayId
    },
    /* "hasPart":[{}], -- google recommend -- can be done via linakage?*/
    /* "isPartOf":[{}], -- google recommend */
    /* "sameAs":{} - google recommend */
    /* "variableMeasured":{} - google recommend */
    "isAccessibleForFree": input.accessibility.access.accessRequestCost = "Free" ? true : false,
    "dateCreated": $to_iso8601(input.required.issued),
    "distribution": {
        "@type":"DataDownload",
        "name": input.accessibility.access.accessService,
        "contentUrl": "https://somehwere.com"
    },
    "keywords": input.summary.keywords ~> $to_str,
    "license": input.accessibility.access.accessRights
    /* "measuredmentTechnique":{} - google recommend */

}
)
