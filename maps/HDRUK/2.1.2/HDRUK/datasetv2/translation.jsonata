(

$nullable_values := [""," ","\"","Not Applicable","Not Available",
                     "Not applicable",
                     "NOT AVAILABLE","NOT APPLICABLE",[null]];

$deliveryLeadTime_values := [
                  "LESS 1 WEEK",
                  "1-2 WEEKS",
                  "2-4 WEEKS",
                  "1-2 MONTHS",
                  "2-6 MONTHS",
                  "MORE 6 MONTHS",
                  "VARIABLE",
                  "NOT APPLICABLE",
                  "OTHER"
];

$to_str := function($x){
  $type($x)='array' ? (
      $map(
          $x,
          function($item){
              $type($item)='array' ? $to_str($item) : $item
          }
      ) ~> $filter(function($x) { $x != null and $x != "" }) ~> $join(",")
  ) : $x
};

$null_string := function($x){
  ($x in $nullable_values or $type($x) != "string" or $not($exists($x)) ) ? null : $x
};

$fixAccrualPeriodicityValues := {
  '1 Month Cycle':"MONTHLY",
  'IN PROGRESS':"OTHER",
  'Updated at least weekly':"WEEKLY",
  'updated annually':"ANNUAL"
};

$fixAccrualPeriodicity := function($x){
  $x in $keys($fixAccrualPeriodicityValues) ? $lookup($fixAccrualPeriodicityValues,$x): $x
};

$fixTimeLag := function($x){
  $x = '6 MONTHS PLUS' ? "MORE 6 MONTHS" : $x
};

$fixDeliveryLeadTime := function($x){
  $not($x in deliveryLeadTime_values) ? null : $x
};

$doi_matcher := /10.\d{4,9}[-._;()/:a-zA-Z0-9]+(?=[\s,/]|$)/i;

$extract_doi := function($x){
  $match($x,$doi_matcher) ~> 
  $map(function($m){
    $m.match
  }) ~> 
  $join(",") 
};

$get_date := function($x){
  $type($x) = "number" ? $fromMillis($x) : $fromMillis($toMillis($x,'[D01]/[M01]/[Y0001]'))
};

$force_url := function($x){
    $x = /^http(s)?:\/\// ? $x : null
};

input ~> |$| 
{
    "issued": $get_date(issued),
    "modified": $get_date(modified),
    "revisions": revisions,
    "version": version ~> $null_string,
    "summary": summary ~> |$| /*copy summary over and transform it*/
    {
      /*loop over all publisher fields and set blank fields to null */
      "publisher": {
        "identifier": publisher.identifier  ~> $null_string,
        "name": publisher.name  ~> $null_string,
        "logo": publisher.logo  ~> $null_string,
        "description": publisher.description  ~> $null_string,
        "contactPoint": publisher.contactPoint  ~> $null_string,
        "memberOf": publisher.memberOf  ~> $null_string
      },
      "keywords": keywords ~> $to_str ~> $null_string, 
      "alternateIdentifiers": alternateIdentifiers ~> $to_str ~> $null_string,
      "doiName": doiName ~> $to_str ~>  $extract_doi ~> $null_string 
    }|,
    "documentation": documentation ~> |$| /*copy documentation over and transform it*/
    {
      "description": $length(description)=0 ? null : description,
      "associatedMedia": associatedMedia ~> $to_str ~> $null_string,
      "isPartOf": isPartOf ~> $to_str ~> $null_string
    }|,
    "coverage": $each(coverage, function($v, $k) { 
      {$k: $v  ~> $to_str ~> $null_string}  /*something up with spacial coverage*/
    }) ~> $merge(),
    "provenance": {
      "origin": $each(provenance.origin, function($v, $k) { 
        {$k: $v  ~> $to_str ~> $null_string}    
      }) ~> $merge(),
      "temporal": $each(provenance.temporal, function($v, $k) { 
        {$k: $v  ~> $to_str ~> $null_string}    
      }) ~> $merge() ~> |$|
         {
          "accrualPeriodicity": accrualPeriodicity ~> $fixAccrualPeriodicity ~> $null_string,
          "timeLag": timeLag ~> $fixTimeLag 
         }|
    },
    "accessibility": {
      "usage": $each(accessibility.usage, function($v, $k) { 
        {$k: $v  ~> $to_str ~> $null_string}  
      }) ~> $merge(),
      "access": $each(accessibility.access, function($v, $k) { /*isReferencedBy should be doi */ 
        {$k: $v ~> $to_str ~> $null_string}  
        }) ~> $merge() ~> |$|
          {
          "deliveryLeadTime": deliveryLeadTime ~> $fixDeliveryLeadTime
          }|,
      "formatAndStandards": $each(accessibility.formatAndStandards, function($v, $k) { 
        {$k: $v  ~> $to_str ~> $null_string}   
      }) ~> $merge()
    },
    "enrichmentAndLinkage": enrichmentAndLinkage ~> |$| 
    {
      "qualifiedRelation": qualifiedRelation ~> $to_str ~> $null_string,
      "derivation": derivation ~> $to_str ~> $null_string,
      "tools": tools ~> $to_str ~> $null_string
    }|,
    "observations": observations ? [$map(input.observations,function($r) {
      {
        "observedNode": $uppercase($r.observedNode),
        "measuredValue": $r.measuredValue,
        "observationDate": $r.observationDate,
        "measuredProperty": $r.measuredProperty,
        "disambiguatingDescription": $r.disambiguatingDescription
      }
    })] : [],
    "structuralMetadata": [$map($$.extra.structuralMetadata, function($smd) { 
      $count($keys($smd)) > 0 ? 
      {
        "name":$smd.tableName,
        "description":$smd.tableDescription,
        "elements":[
          {
            "name": $smd.columnName,
            "description": $smd.columnDescription,
            "dataType": $smd.dataType,
            "sensitive": $smd.sensitive ~> $null_string = null ? false : $smd.sensitive
          }
        ]
      } : null
    })] 
},
['datasetv2.identifier','datasetv2.issued','datasetv2.modified'] 
|
)